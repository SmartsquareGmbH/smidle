buildscript {
    ext {
        javaVersion = JavaVersion.VERSION_11
        gradleVersion = "6.5.1"
        kotlinVersion = "1.3.72"
        springBootVersion = "2.3.1.RELEASE"
        ktlintGradleVersion = "9.2.1"
        detektVersion = "1.10.0"
        jacksonVersion = "2.11.0"
        jibGradlePluginVersion = "2.1.0"
        postgresVersion = "42.2.14"
        flywayVersion = "6.5.0"
    }

    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"

        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintGradleVersion"

        classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:$jibGradlePluginVersion"
    }
}

apply plugin: "kotlin"
apply plugin: "kotlin-jpa"
apply plugin: "kotlin-spring"
apply plugin: "kotlin-kapt"
apply plugin: "org.springframework.boot"
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "io.spring.dependency-management"
apply plugin: "com.google.cloud.tools.jib"

group = "de.smartsquare"
version = "0.0.1-SNAPSHOT"
java.sourceCompatibility = javaVersion

repositories {
    gradlePluginPortal()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "org.flywaydb:flyway-core:$flywayVersion"

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    kapt "org.springframework.boot:spring-boot-configuration-processor"

    runtimeOnly "org.postgresql:postgresql:$postgresVersion"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "com.vaadin.external.google", module: "android-json"
        exclude module: "mockito-core"
    }
}

detekt {
    config = files("config/detekt/config.yml")

    buildUponDefaultConfig = true

    reports {
        xml.destination = file("$buildDir/reports/detekt/detekt.xml")
        html.destination = file("$buildDir/reports/detekt/detekt.html")
    }
}

kapt {
    correctErrorTypes = true
}

test {
    useJUnitPlatform {
        excludeEngines "junit-vintage"
    }
}

jib {
    container.mainClass = "de.smartsquare.smidle.SmidleApplicationKt"

    to {
        image = "docker.pkg.github.com/smartsquaregmbh/smidle/smidle-rest-api:latest"
    }

    from {
        image = "gcr.io/distroless/java@sha256:00b6c5ad8f50f6d4434d9557900ab89a84d349264cc07bd842f1be1e5f2aa827"
    }
}
